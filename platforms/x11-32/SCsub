import os
import shutil
from itertools import chain
from pathlib import Path
from SCons.Errors import UserError


Import("env")


env["bits"] = "32"
env.Append(CFLAGS="-m32")
env.Append(LINKFLAGS="-m32")


### Godot binary (to run tests) ###


if not env["godot_binary"]:
    env["godot_binary"] = File("godot.x11.opt.debug.32")
    env.Command(
        env["godot_binary"],
        None,
        "curl -L %s/godot.x11.opt.debug.32 -o ${TARGET} && chmod 750 ${TARGET}"
        % env["godot_release_base_url"],
    )
env.NoClean(env["godot_binary"])


### Python interpreter ###

cpython_src = Dir("cpython")
env.Command(
    cpython_src,
    None,
    "git clone https://github.com/python/cpython.git --depth=1 --branch=v3.7.1 --single-branch ${TARGET}",
)
env.NoClean(cpython_src)

cpython_build = Dir("cpython_build")
# TODO: allow to compile cpython with `--with-pydebug` ?
# Compile CPython and install cffi through pip
env.Command(
    cpython_build,
    cpython_src,
    "cd ${SOURCE} && " + "echo Configuring CPython... && "
    "1>/dev/null ./configure --enable-shared --prefix=${TARGET.get_abspath()} --with-openssl=/usr && "
    + "echo Building CPython... && "
    "1>/dev/null make -j4 && "
    + "echo Installing CPython in ${TARGET.get_abspath()}... && "
    "1>/dev/null make install && "
    # TODO: still useful to install cffi ?
    + "LD_LIBRARY_PATH=${TARGET.get_abspath()}/lib ${TARGET.get_abspath()}/bin/pip3 install cffi",
)
env.NoClean(cpython_build)


def add_cpython_to_build_dir(env, target, cpython_build):
    def c(subpath=""):
        return os.path.join(cpython_build.abspath, *subpath.split("/"))

    def p(subpath=""):
        return os.path.join(target.abspath, "pythonscript", *subpath.split("/"))

    os.mkdir(p())

    open(p(".gdignore"), "w").close()

    if os.path.isdir(c("include")):
        # Windows build of CPython doesn't contain include dir
        shutil.copytree(c("include"), p("include"))

    # Remove __pycache__ to save lots of space
    for root, dirs, files in os.walk(c("lib")):
        if "__pycache__" in dirs:
            shutil.rmtree(os.path.join(root, "__pycache__"))

    shutil.copytree(c("bin"), p("bin"))

    shutil.copytree(c("lib"), p("lib"))
    if env["compressed_stdlib"]:
        shutil.move(p("lib/python3.7"), p("lib/tmp_python3.7"))
        os.mkdir(p("lib/python3.7"))
        shutil.move(p("lib/tmp_python3.7/lib-dynload"), p("lib/python3.7/lib-dynload"))
        shutil.move(
            p("lib/tmp_python3.7/site-packages"), p("lib/python3.7/site-packages")
        )
        shutil.make_archive(
            base_name=p("lib/python37"), format="zip", root_dir=p("lib/tmp_python3.7")
        )
        shutil.rmtree(p("lib/tmp_python3.7"))


def add_pythonscript_stuff_to_build_dir(
    env, target, libpythonscript, _godot_module, godot_module
):
    def p(subpath=""):
        return os.path.join(target.abspath, "pythonscript", *subpath.split("/"))

    def symlink_on_need(src, trg):
        if os.path.exists(trg):
            print(f"dev_dyn: skip creating SymLink, {trg} already exists")
        else:
            print(f"dev_dyn: SymLink {src} -> {trg}")
            os.symlink(src, trg)

    if env["dev_dyn"]:
        symlink_on_need(libpythonscript.abspath, p(libpythonscript.name))
        symlink_on_need(
            _godot_module.abspath,
            p("lib/python3.7/site-packages/" + _godot_module.name),
        )
        symlink_on_need(godot_module.abspath, p("lib/python3.7/site-packages/godot"))

    else:
        print(f"Copy {libpythonscript.path} -> {p('lib/')}")
        shutil.copy(libpythonscript.path, p())

        print(f"Copy {_godot_module.path} -> {p('lib/')}")
        shutil.copy(_godot_module.path, p("lib/python3.7/site-packages/"))

        dst_dir = Path(p("lib/python3.7/site-packages/godot"))
        src_dir = Path(godot_module.path)
        print(f"Copy {src_dir} -> {dst_dir}")
        for src_item in chain(
            src_dir.glob("**/*.py"), src_dir.glob("**/*.so"), src_dir.glob("**/*.pxd"),
        ):
            dst_item = dst_dir / src_item.relative_to(src_dir)
            dst_item.parent.mkdir(parents=True, exist_ok=True)
            shutil.copy(str(src_item), str(dst_item))


env["add_cpython_to_build_dir"] = add_cpython_to_build_dir
env["add_pythonscript_stuff_to_build_dir"] = add_pythonscript_stuff_to_build_dir
env["backend_dir"] = cpython_build
env.Append(CFLAGS=["-DBACKEND_CPYTHON"])
env.Append(CFLAGS=["-I%s/include/python3.7m/" % cpython_build.path])
env.Append(LIBPATH=["%s/lib" % cpython_build.path])
env.Append(LIBS=["python3.7m"])
env.Append(LINKFLAGS=["-Wl,-rpath,'$$ORIGIN/lib'"])
